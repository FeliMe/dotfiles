SUBSCRIPTION_ID := "98fd3145-0fad-4970-9164-08dfe291b32f"
VM_NAME := "dotfiles-vm"

export UV_PRERELEASE := "allow"

default:
    @just --list

run:
    @just setup
    @just create
    echo "Waiting 20 seconds for the VM to be ready..."
    @sleep 20
    @just apply
    @just clean

install:
    uv venv --python 3.10
    uv pip install azure-cli

setup: install
    #!/usr/bin/env zsh
    if ! uv run az account show &>/dev/null; then
        uv run az login
    fi
    uv run az account set --subscription {{SUBSCRIPTION_ID}}

create:
    #!/usr/bin/env zsh
    mise use terraform
    terraform init -upgrade
    terraform plan -out main.tfplan
    terraform apply -auto-approve main.tfplan

    resource_group_name=$(terraform output -raw resource_group_name)
    uv run az vm list \
        --resource-group $resource_group_name \
        --query "[].{\"VM Name\":name}" \
        -o table

ssh:
    #!/usr/bin/env zsh
    resource_group_name=$(terraform output -raw resource_group_name)
    uv run az vm user update \
        --resource-group $resource_group_name \
        --name {{VM_NAME}} \
        --username fernando \
        --ssh-key-value "$(ssh-add -L | head -n 1)"

destroy:
    terraform plan -destroy -out main.destroy.tfplan
    terraform apply -auto-approve main.destroy.tfplan

remake: destroy create

connect:
    #!/usr/bin/env bash
    resource_group=$(terraform output -raw resource_group_name)
    uv run az ssh vm \
        --resource-group $resource_group \
        --name {{VM_NAME}} \
        --local-user fernando

apply:
    #!/usr/bin/env bash
    resource_group=$(terraform output -raw resource_group_name)
    uv run az ssh vm \
        --resource-group $resource_group \
        --name {{VM_NAME}} \
        --local-user fernando \
            -- \
            -t 'sh -c "$(curl -fsLS get.chezmoi.io)" -- init --apply fepegar; zsh'
