PROJECT_ID := "virtual-machine-451721"
VM_NAME := "dotfiles-vm"
ZONE := "us-central1-a"

default:
    @just --list

run:
    just setup
    just create
    echo "Waiting 20 seconds for the VM to be ready..."
    sleep 20
    just apply
    just destroy

install:
    mise use gcloud terraform

setup: install
    #!/usr/bin/env zsh
    if ! gcloud auth application-default print-access-token &>/dev/null; then
      gcloud auth application-default login
    fi
    if ! gcloud auth print-access-token &>/dev/null; then
      gcloud auth login
    fi
    gcloud config set project {{PROJECT_ID}}
    gcloud services enable compute.googleapis.com

create:
    terraform init
    terraform apply -auto-approve

destroy:
    terraform destroy -auto-approve

remake: destroy create

connect:
    gcloud compute ssh --zone {{ZONE}} {{VM_NAME}}

apply:
    gcloud compute ssh --zone {{ZONE}} {{VM_NAME}} --command 'sh -c "$(curl -fsLS get.chezmoi.io)" -- init --apply fepegar'
    @just connect
